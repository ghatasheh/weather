apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: "$rootDir/config/android_targets.gradle"

android {
    compileSdk targets.compileSdk

    defaultConfig {
        applicationId "com.hisham.weather"
        minSdk targets.minSdk
        targetSdk targets.targetSdk
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        all {
            buildConfigField "String", "API_URL", "\"https://api.openweathermap.org/data/2.5/\""
        }

        debug {
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        useIR = true
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    // Features
    implementation project(path: ':features:home:data')
    implementation project(path: ':features:home:presentation')
    implementation project(path: ':features:home:domain')

    // Libraries
    implementation project(path: ':libraries:navigation')
    implementation project(path: ':libraries:threading')
    implementation project(path: ':libraries:design')

    // Jetpack
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_extensions_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_viewmodel_ktx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_runtime_ktx_version"

    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"

    implementation "androidx.activity:activity-compose:$activity_compose"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$viewmodel_compose"

    implementation "androidx.navigation:navigation-compose:$compose_navigation_version"
    implementation "androidx.hilt:hilt-navigation-compose:$compose_hilt_navigation_version"

    implementation "com.google.accompanist:accompanist-permissions:$accompanist"

    // Kotlin
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // DI
    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_version"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.google.code.gson:gson:$gson_version"

    // Location
    implementation 'com.google.android.gms:play-services-location:18.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.5.2-native-mt'

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    implementation "androidx.hilt:hilt-work:$work_hilt_version"
    kapt "androidx.hilt:hilt-compiler:$work_hilt_version"

    // Utils
    implementation "com.jakewharton.timber:timber:$timber_version"

    // Test
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "io.mockk:mockk:1.12.0"

    androidTestImplementation "io.mockk:mockk-android:1.12.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}